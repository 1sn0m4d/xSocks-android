apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

if(file('../sign.gradle').exists()) {
    apply from: '../sign.gradle'
} else {
    logger.error('no such file: ../sign.gradle')
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"
    defaultConfig {
        applicationId "io.github.xsocks"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 120
        versionName '1.2.0'
    }
    lintOptions {
        abortOnError true
        disable 'MissingTranslation'
    }
    buildTypes {
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
                logger.error('signing error: ' + ex)
            }
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                appendVersionNameVersionCode(variant, output, defaultConfig)
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    productFlavors {
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

def appendVersionNameVersionCode(variant, output, defaultConfig) {
    def versionName = defaultConfig.versionName
    if(variant.buildType.name == android.buildTypes.debug.name) {
        versionName += "-beta"
    }
    def file = output.outputFile
    output.outputFile = new File(file.parent, "Xsocks.apk".replace(".apk", "-v" + versionName + ".apk"))
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'io.reactivex:rxjava:1.0.10'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'io.reactivex:rxjava-async-util:0.21.0'
    compile('com.mikepenz.materialdrawer:library:2.9.7@aar') {
        transitive = true
    }
    compile 'com.nispok:snackbar:2.10.8'
    compile 'commons-net:commons-net:3.3'
    compile 'dnsjava:dnsjava:2.1.7'
    compile 'com.google.code.gson:gson:2.3.1'
}
